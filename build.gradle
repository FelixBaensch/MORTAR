import java.util.regex.Matcher

plugins {
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.8'
}

group 'de.unijena.cheminf'
version '0.1'

sourceCompatibility = 1.11

repositories {
    mavenCentral()
}

dependencies {
    testImplementation group: 'junit', name: 'junit', version: '4.13'
    implementation group: 'org.openscience.cdk', name: 'cdk-bundle', version: '2.3'
}

javafx {
    version = '11.0.2'
    modules = ['javafx.controls']
}

mainClassName = 'de.unijena.cheminf.mortar.main.Main'

/*
* Merges the exact enumeration of all library jars into one single path giving all the files and subfolders in 'lib'.
* This is necessary because lines that are too long cause errors in batch files.
* The unix script still has to be tested!
*/
startScripts {
    doLast {
        windowsScript.text = windowsScript.text.replaceAll('set CLASSPATH=.*', 'set CLASSPATH=.;%APP_HOME%/lib/*')
        unixScript.text = unixScript.text.replaceAll('CLASSPATH=\\Q$\\EAPP_HOME/lib.*', Matcher.quoteReplacement('CLASSPATH=$APP_HOME/lib/*'))
    }
}

jar {
    manifest {
        attributes 'Main-Class': mainClassName
    }
}

task fatJar(type: Jar) {
    manifest {
        attributes 'Implementation-Title': 'MORTAR Fat Jar File',
        'Implementation-Version': archiveVersion,
        'Main-Class': mainClassName
    }
    archiveBaseName = project.name + '-fat'
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    with jar
}

artifacts {
    archives fatJar
}